buildscript {
    dependencies {
        classpath 'com.h2database:h2:2.1.214'
        classpath 'mysql:mysql-connector-java:8.0.31'
    }
}

plugins {
    id 'org.springframework.boot' version '2.7.5'
    id "org.flywaydb.flyway" version "9.6.0"
    id 'java'
    id 'jacoco'
    id 'checkstyle'
    id 'pmd'
    id 'project-report'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id "com.github.spotbugs" version "5.0.13"
}
apply plugin: 'io.spring.dependency-management'

group = 'com.example'
version = '0.0.1-SNAPSHOT'
targetCompatibility = '11'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'de.codecentric:spring-boot-admin-starter-server'
    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'org.flywaydb:flyway-core'
    compileOnly 'org.projectlombok:lombok:1.18.8'
    runtimeOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'mysql:mysql-connector-java'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok:1.18.8'
    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude(group: 'org.junit.vintage')
    }
}

dependencyManagement {
    imports {
        mavenBom "de.codecentric:spring-boot-admin-dependencies:2.3.1"
    }
}

jacoco {
    toolVersion = "0.8.6"
}

jacocoTestReport {
    sourceSets sourceSets.main
    reports {
        xml.enabled true
        html.enabled true
        csv.enabled false
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    finalizedBy jacocoTestReport
}


// test only classes with @IntegrationTest
task jacocoIntegrationTestReport(type: JacocoReport) {
    executionData file("build/jacoco/integrationTest.exec")
    sourceSets sourceSets.main
    reports {
        xml.enabled true
        html.enabled false
        csv.enabled false
    }
}
task integrationTest(type: Test) {
    useJUnitPlatform {
        includeTags "integrationTest"
    }
    finalizedBy jacocoIntegrationTestReport
}

// test only classes with @UnitTest
task jacocoUnitTestReport(type: JacocoReport) {
    executionData file("build/jacoco/unitTest.exec")
    sourceSets sourceSets.main
    reports {
        xml.enabled true
        html.enabled false
        csv.enabled false
    }
}
task unitTest(type: Test) {
    useJUnitPlatform {
        includeTags "unitTest"
    }
    finalizedBy jacocoUnitTestReport
}


// 静的解析
checkstyle {
    toolVersion '8.38'
    ignoreFailures = true
    configFile = rootProject.file('config/checkstyle/google_checks.xml')
}

pmd {
    toolVersion '6.13.0'
    ignoreFailures = true
    ruleSets = [
            "category/java/bestpractices.xml",
            "category/java/codestyle.xml",
            "category/java/design.xml",
            "category/java/documentation.xml",
            "category/java/errorprone.xml",
            "category/java/multithreading.xml",
            "category/java/performance.xml",
            "category/java/security.xml"
    ]
}

spotbugs {
    toolVersion = '4.7.1'
}

flyway {
    url = 'jdbc:h2:file:./target/foobar'
    user = 'sa'
}
